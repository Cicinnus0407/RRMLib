apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


version = "1.0.1"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"


    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.1.1'
    testCompile 'junit:junit:4.12'
    //RxAndroid
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //RxJava2
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    //Retrofit2的RxJava适配
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    //Retrofit2
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    //RxJava2Gson适配
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    //网络日志拦截
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
}



def siteUrl = 'https://github.com/Cicinnus0407/RRMLib'      // 项目的主页
def gitUrl = 'https://github.com/Cicinnus0407/RRMLib.git'   // Git仓库的url
group = "com.cicinnus.RRMLib"                           // Maven Group ID for the artifact，
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'RxJava2+Retrofit+MVP封装库'    //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'cicinnus'        //填写的一些基本信息
                        name 'cicinnus'
                        email 'cicinnus0407@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = "UTF-8" //添加编码方式，才不会报编码错误
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}




Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
//配置jCenter
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "RRMLib"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            name = '1.0.0'
            desc = 'RxJava2+Retrofit2+MVP Lib'
            released  = new Date()
            vcsTag = 'v1.0.0'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}
